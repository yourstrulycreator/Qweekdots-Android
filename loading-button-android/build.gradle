apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

// Bintray depends on this global variable to set the library version!
version = "2.2.0"
group = 'br.com.simplepass'

android {
    compileSdkVersion 29

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions{
        abortOnError false
    }
    sourceSets {
        main.java.srcDirs += 'src/main/java'
    }
}

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayKey')) {
    bintray.user = project.bintrayUser
    bintray.key = project.bintrayKey
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.3.61"
    implementation 'androidx.appcompat:appcompat:1.1.0'

    testImplementation 'org.mockito:mockito-core:2.23.4'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0"
    testImplementation 'org.robolectric:robolectric:4.1'
    testImplementation 'androidx.test:core:1.1.0'
    testImplementation 'junit:junit:4.12'

}
repositories {
    mavenLocal()
    mavenCentral()
}
